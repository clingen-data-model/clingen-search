prefix gci: <http://gci.clinicalgenome.org/>

construct
{
  ?classification :sepio/has-evidence-line ?experimentalEvidenceLine .
  
  ?experimentalEvidenceLine a :sepio/OverallExperimentalEvidenceLine ;
  :sepio/has-evidence-item ?experimentalEvidenceCriterionAssessment ;
  :sepio/evidence-line-strength-score ?experimentalEvidenceTotal .

  ?experimentalEvidenceCriterionAssessment a :sepio/ExperimentalEvidenceCriterionAssessment ;
  :sepio/has-evidence-line ?functionalEvidenceLine, ?functionalAlterationEvidenceLine, ?modelAndRescueEvidenceLine .

  ?functionalEvidenceLine a :sepio/FunctionalEvidenceLine ;
  :sepio/has-evidence-item ?functionalEvidenceCriterionAssessment ;
  :sepio/evidence-line-strength-score ?functionTotal .

  ?functionalEvidenceCriterionAssessment a :sepio/FunctionalCriterionAssessment .

  ?functionalAlterationEvidenceLine a :sepio/FunctionalAlterationEvidenceLine ;
  :sepio/has-evidence-item ?functionalAlterationCriterionAssessment ;
  :sepio/evidence-line-strength-score ?functionalAlterationTotal .

  ?functionalAlterationCriterionAssessment a :sepio/FunctionalAlterationCriterionAssessment .  

  ?modelAndRescueEvidenceLine a :sepio/ModelAndRescueEvidenceLine ;
  :sepio/has-evidence-item ?modelAndRescueCriterionAssessment ;
  :sepio/evidence-line-strength-score  ?modelsRescueTotal .

  ?modelAndRescueCriterionAssessment a :sepio/ModelAndRescueCriterionAssessment .
}
where
{
  #### Bind evidence points
  
  ?classification a gci:provisionalClassification .
  ?classification gci:classificationPoints ?pointsTree .

  ?pointsTree gci:experimentalEvidenceTotal ?experimentalEvidenceTotal .

  ?pointsTree gci:function ?functionTree .
  ?functionTree gci:pointsCounted ?functionTotal .

  ?pointsTree gci:functionalAlteration ?functionalAlterationTree .
  ?functionalAlterationTree gci:pointsCounted ?functionalAlterationTotal .

  ?pointsTree gci:modelsRescue ?modelsRescueTree .
  ?modelsRescueTree gci:pointsCounted ?modelsRescueTotal .

  ?pointsTree gci:segregation ?segregationTree .
  ?segregationTree gci:pointsCounted ?segregationTotal .

  #### Bind criterion assessment and evidence line identifiers
  BIND(IRI(CONCAT(str(?classification), "_experimental_evidence_line"))
       AS ?experimentalEvidenceLine) .
  BIND(IRI(CONCAT(str(?classification), "_experimental_evidence_criterion_assessment"))
       AS ?experimentalEvidenceCriterionAssessment) .
  BIND(IRI(CONCAT(str(?classification), "_functional_evidence_line"))
       AS ?functionalEvidenceLine) .
  BIND(IRI(CONCAT(str(?classification), "_functional_evidence_criterion_assessment"))
       AS ?functionalEvidenceCriterionAssessment) .
  BIND(IRI(CONCAT(str(?classification), "_functional_alteration_evidence_line"))
       AS ?functionalAlterationEvidenceLine) .
  BIND(IRI(CONCAT(str(?classification), "_functional_alteration_criterion_assessment"))
       AS ?functionalAlterationCriterionAssessment) .
  BIND(IRI(CONCAT(str(?classification), "_model_rescue_evidence_line"))
       AS ?modelAndRescueEvidenceLine) .
  BIND(IRI(CONCAT(str(?classification), "_model_rescue_criterion_assessment"))
       AS ?modelAndRescueCriterionAssessment) .
}
